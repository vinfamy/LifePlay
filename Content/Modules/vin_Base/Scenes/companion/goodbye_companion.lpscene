WHAT: all
WHERE: all
WHEN: 23-6
WHO: Companion = getCompanionAny()
OTHER: none

    sceneStart()
    CurrentCompanion.show(2)
    If CurrentCompanion.isCreature()
        "Time flies. I have enjoyed taking <CurrentCompanion.name> out for a walk but it's time to head home."         
    Else
        WalkedHome = false
        MyHome = false

        Dating = CurrentCompanion.IsDating()

        "Time flies. I have enjoyed spending time with <CurrentCompanion.name> but it's time to part."
        If CurrentCompanion.isRelative()
            "It has been good catching up with my <CurrentCompanion.relationship>."
        Elseif CurrentCompanion.IsDominantSex(Player)
            If  !isAtHome() && [Dating || CurrentCompanion:interpersonal + CurrentCompanion:attractiontoplayer > Random(0, 100)]
                CurrentCompanion::"I'll be glad to walk you home safe. It's only my duty."
                0:: "Be walked home"
                1:: "Decline"
            
                If 0
                    "How can I decline such a nice offer? <CurrentCompanion.name> walked me back to my apartment."
                    moveTo(Home)
                    setBackground(street)
                    passTime(0.1, 0.5)
                    "A while later ..."
                    CurrentCompanion(Happy)::"Here we are. I'll see you later then"
                    0:: "Invite <CurrentCompanion.him_or_her> inside"
                    1:: "Say goodbye"
                    
                    If 0
                        WalkedHome = true
                        MyHome = true
                        If Dating
                            Player(Flirty):: "Baby, why don't you stay for the night?"
                            CurrentCompanion(Flirty):: "Of course, love."                
                        Else
                            Player(Flirty):: "Wait ... Do you want to come inside and chill for a while? You were so kind in walking me all the way back after all."
                            CurrentCompanion(Happy):: "Of course, it'll be my pleasure."
                        Endif
                    Else
                        Player(Happy):: "Goodbye. I had a great time today. See you soon!"
                        "We went our separate ways."
                    Endif
                Else
                    Player(Surprised)::"It's not neccessary! Thank you for the nice offer though."
                    "We went our separate ways."
                Endif
            Endif
        Else
            "Should I offer to walk <CurrentCompanion.name> home?"
            0:: "Yes"
            1:: "No"
            
            If 0
                Player(Happy)::"Do you want me to walk you home? I just want to make sure you're safe."
                If Dating || CurrentCompanion:attractiontoplayer > Random(0, 100) || CurrentCompanion:rapportwithplayer > Random(0, 100)
                    CurrentCompanion(Happy)::"Oh, thank you! That's very nice of you."
                    "I chivalrously walked with <CurrentCompanion.name> back to <CurrentCompanion.his_or_her> place."
                    Loc = CurrentCompanion.getBuilding(home)
                    moveTo(Loc)
                    setBackground(street)
                    passTime(0.1, 0.5)
                    "A while later ..."
                    Player:: "So here it is, we arrived. I guess I'll see you soon ..."
                    If Dating || CurrentCompanion:attractiontoplayer > Random(0, 100)
                        If Dating
                            CurrentCompanion(Flirty)::"Baby, don't leave just yet. Do you want to stay the night?"  
                        Else
                            CurrentCompanion(Flirty)::"Wait, don't leave just yet. I feel guilty making you walk all the way here. Do you want to come in for tea or something?"                
                        Endif
                        "Come in?"
                        0:: "Yes"
                        1:: "No"
                        
                        If 0
                            If Dating
                                Player(Happy):: "Of course, love."
                            Else
                                Player(Happy):: "Of course, I don't mind staying for a while."
                            Endif
                            CurrentCompanion(Excited):: "Excellent! Hang on a minute. Let me just open the door."
                            WalkedHome = true
                        Else
                            CurrentCompanion:attractiontoplayer *= 1.05
                            Player(Happy):: "That's okay. I'd better get going. See you!"
                            "We went our separate ways."
                        Endif
                    Endif
                Else
                    CurrentCompanion(Surprised)::"No, thank you. I wouldn't want to bother you. I'm happy to walk home myself."
                    "I couldn't really insist on it, so we waved goodbye and went our separate ways."
                Endif
            Else
                "I decided not to offer to walk <CurrentCompanion.him_or_her> back. We said goodbye and parted ways."
            Endif
        Endif

        If WalkedHome
            setBackground(home)
            "We're now inside the apartment."
            choice = -1    
            ate = false
            hadSex = false    
            Vanilla = IsModEnabled(vin_VanillaRealistic)
                
            While choice < 5
                Kissed = false
                "What should I suggest to <CurrentCompanion.name> that we do?"
                0:: "Chat"
                1:: !ate:: "Eat something together"
                2:: "Watch a movie"
                3:: "Drink alcohol"
                4:: !hadSex:: "Kiss <CurrentCompanion.him_or_her>"
                5:: MyHome:: "Politely ask <CurrentCompanion.him_or_her> to leave"
                6:: !MyHome:: "Leave"
                7:: Dating && !IsPlayerMarried():: "Break up!"
                8:: Dating && IsPlayerMarried():: "Divorce!"
                
                If 5 || 6
                    "We said goodbye and went our ways."
                Elseif 7
                    Player(Sad):: "Baby, I'm afraid this isn't working ..."
                    "It was a difficult conversation to have, but I did it! I broke up with <CurrentCompanion.name>. We just weren't meant for each other ..."
                    mood -= 50
                    LoseDating()
                Elseif 8
                    Player(Sad):: "Baby, I'm afraid this marriage isn't working ..."
                    "It was a difficult conversation to have, but I did it! We agreed to divorce. We just weren't meant for each other ..."
                    mood -= 100
                    Divorce()
                    LoseDating()
                    If Player.isDominantSex(Dating) && money > 1000
                        "Unfortunately for me, the court awarded half of my hard-earned fortune to <Dating.name> ... What a disaster of a marriage that was ..."
                        money -= money*0.5
                    Endif
                Elseif Dating && CurrentCompanion:attractiontoplayer < Random(-100, 20) && !isPlayerMarried()
                    CurrentCompanion(Sad):: "Baby, I'm afraid this isn't working ..."
                    "<CurrentCompanion.name> did it! <CurrentCompanion.He_or_She> broke up with me. Maybe it was something I did ... or we simply weren't meant for each other."
                    mood -= 100
                    LoseDating()
                    choice = 7
                Else
                    If 0
                        "We simply sat and chat for a while ..."
                        passTime(0.1, 0.2)
                    Elseif 1
                        "We made some quick snacks and enjoyed them together ..."
                        ate = true
                        passTime(0.2, 0.4)
                    Elseif 2
                        "We watched a movie together ..."
                        passTime(1, 2)
                    Elseif 3
                        "We had a drink together ..."
                        passTime(0.1, 0.2)
                        Player:intoxication += Random(0,10)
                        CurrentCompanion:intoxication += Random(0,10)
                    Elseif 4
                        Kissed = true
                    Endif

                    If Kissed || [CurrentCompanion.isInterestedIn(Player) && !hadSex && CurrentCompanion:attractiontoplayer > Random(0, 200)]
                        AnimatePair(Player, CurrentCompanion, Kissing)
                        Player(Kiss):: "..."
                        CurrentCompanion(Kiss):: "..."
                        If Dating
                            "We kissed. It started slow but soon became more passionate. It was obvious what we both wanted ..."
                        Else
                            "We had a kiss, an awkward one at first, but soon moving towards a more passionate direction."
                        Endif
                        "Should I let this escalate further?"
                        0:: Vanilla:: "Make out"
                        1:: "Stop"
                        
                        If 0
                            If CurrentCompanion.isDating() || [CurrentCompanion.isInterestedIn(Player) && [CurrentCompanion.IsDominantSex(Player) || CurrentCompanion:attractiontoplayer + CurrentCompanion:perversion > Random(50, 200) || CurrentCompanion:intoxication > 80]]
                                "Our kiss got more and more steamy and soon we were making out, our hands all over each other."
                                If TfGame() && CurrentCompanion.isMale()
                                    "I'm a man trapped inside a woman's body. I don't think I'm gay, but here I am, about to hook up with my male date."
                                Endif
                                hadSex = true
                                Player:perversion += Random(0, 1)
                                Sex(Player, CurrentCompanion)
                                CurrentCompanion.show(2)
                                "The sex was amazing. We put our clothes back on afterwards and continued our date."
                                passTime(0.2, 0.5)
                            Else
                                "Unfortunately for me, <CurrentCompanion.name> didn't seem interested in letting things escalate any further and pulled away."
                            Endif
                        Else
                            "I decided to stop it there and not go any further."
                        Endif
                    Elseif Random(0, 100) < 20
                        If Dating
                            "After a while, we were both exhausted and went to bed together for the night."
                            passTime(4, 8)
                            energy += Random(50, 100)
                            "The next morning ..."
                            If MyHome
                                CurrentCompanion(Happy):: "I've gotta go now, honey. Thanks for letting me stay the night. I can't wait to see you again soon!"
                            Else
                                Player(Happy):: "I've gotta go now, honey. Thanks for letting me stay the night. I can't wait to see you again soon!"
                            Endif
                            CurrentCompanion:attractiontoplayer += Random(0, 1)
                        Else
                            If MyHome
                                CurrentCompanion(Happy):: "I've gotta go now. Thank you for inviting me in."
                                Player(Happy):: "Alright. Thank you for staying. Goodbye!"
                            Else
                                CurrentCompanion(Happy):: "It has been a great day, but I'm getting quite tired now. Do you mind ..."
                                "Getting the message from <CurrentCompanion.name>, I said goodbye."
                            Endif
                            "We parted ways."
                        Endif
                        choice = 5            
                    Endif        
                Endif
            Endwhile
        Endif
    Endif

    sceneEnd()
    endDate()