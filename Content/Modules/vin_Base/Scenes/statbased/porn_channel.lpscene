WHAT: all, -sleep, -nap, -sleep_hotel, -nap_hotel
WHERE: all, -work
WHEN: 17 - 22
WHO: none
OTHER: Random(80, 200) < perversion
    
    SceneStart()
    ChannelStarted = porn_channel.getGlobal()
    
    If ChannelStarted == 0
        "I consider myself a sexually adventurous person who doesn't mind showing some nudity to an audience and can probably perform sexually in front of a camera."
        "I reckon I might just have some success starting a private porn profile on Snapchat. Should I?"
        0:: "Yes"
        1:: "No"
        
        If 0
            "Why not? It's certainly worth a shot. Might be a good way to supplement my income."
            porn_channel.setGlobal(1)
        Else
            "No, I should stop daydreaming and focus on my real job."
            timeout(500, porn_channel)
        Endif
    Else
        "I've got some spare time. Should I make a new video for my porn Snapchat?"
        0:: "Yes"
        1:: "No"
        2:: "Quit Snapchat"
        
        If 0
            "Yes, it's important to upload regularly to keep my audience growing."
            "Should this be a solo video or full-blown intercourse?"
            0:: "Solo"
            1:: "Sex"
            
            If 0
                Random
                    "Just showing them some skin should do."
                    "A video of me pleasuring myself should be enough to keep my subscribers excited."
                EndRandom
            Else
                "A sex scene would be best, but who should I do it with in the video?"
                Actor = getSpecific(Dating)
                If !Actor.isValid()
                    count = 0
                    While count < 10 && Actor:perversion + Actor:attractiontoplayer < 100 
                        Actor = getPerson(true)
                        If Actor.isValid()
                            count += 1
                        Else
                            count = 10
                        Endif
                    Endwhile
                    If count == 10
                        Actor = getSpecific(Dating)
                    Endif
                Endif
                
                If Actor.isValid()
                    Actor.dress()
                    Actor.show(2)
                    "I think <Actor.name> would be the perfect co-star."
                    0:: "Yes"
                    1:: "No"
                    
                    If 0
                        "I managed to convince <Actor.name> to star in the video."
                        setBackground(home)
                        Sex(Actor, Player)
                    Else
                        "On second thought, I'll just stick with a solo video."
                    Endif
                    Actor.hide()
                Else
                    "Unfortunately, I couldn't quite think of any suitable co-star ... A solo video it is then."
                Endif
            Endif
            
            perversion += Random(0, 0.25)
            If Random(0, 100) < perversion
                "The new video turned out to be a success, helping me grow the number of subscribers on Snapchat, which in turn generated a decent amount of revenue"
                money += Random(0.5, 5)*perversion
                mood += 10
            Else
                "Unfortunately, the new video was a miss, not earning many views at all and even less money."
                mood -= 10
            Endif
        Elseif 1
            "I'm too busy right now to film. Let's do that another day."
        Else
            "This thing isn't working out. It's time to shut it down."
            porn_channel.clearGlobal()
            timeout(500, porn_channel)
        Endif
    Endif
    
    SceneEnd()
    timeout(48, porn_channel)